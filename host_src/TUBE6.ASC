10  'Amstrad CPC-Acorn Tube Host Code
20  '(C) 2018 Hoglet, BigEd, Revaldinho
30 DEFINT A-Z
40 DEFREAL f
50 DIM A$(16)
60 ON ERROR GOTO 140
70 MODE 2: INK 0,0 : INK 1,26 : BORDER 0
80 MEMORY HIMEM-128
90 fifo1 = HIMEM+1
100 GOSUB 1080
110 OUT &FC16, 7  :'Select CPU
120 OUT &FC10,&AE :'Tube RESET
130 OUT &FC10,&20
140 WHILE 1=1
150   GOSUB 210
160   'PRINT HEX$(A,2);" ";
170   ON (A+1) GOSUB 540, 510, 570, 510, 510,510, 510,510,510,510,910
180 WEND
190 END
200 'Get FIFO R2 data in A without blocking FIFO R1 traffic
210 WHILE (INP(&FC12) AND &80)=0 
220   CALL fifo1
230 WEND
240 A =INP(&FC13)
250 RETURN
260 'Write parameter T to FIFO R2 without blocking FIFO R1 traffic
270 WHILE (INP(&FC12) AND &40)=0 
280   CALL fifo1
290 WEND
300 OUT &FC13, T
310 RETURN
320 'Write parameter T to FIFO R3 without blocking FIFO R1 traffic
330 WHILE (INP(&FC14) AND &40)=0 
340  CALL fifo1
350 WEND
360 OUT &FC15,T
370 RETURN
380 'Write parameter T to FIFO R4 without blocking FIFO R1 traffic
390 WHILE (INP(&FC16) AND &40)=0 
400   CALL fifo1
410 WEND
420 OUT &FC17,T
430 RETURN
440 'Write string parameter T$ to FIFO R2, terminated with &0D
450 FOR i=1 TO LEN(T$)
460   T= ASC(MID$(T$,i,1)) : GOSUB 270
470 NEXT
480 T=13 : GOSUB 270
490 RETURN
500 'DUMMY Destination for unhandled calls
510 PRINT "[Unhandled Trap]"
520 RETURN
530 'RDCH
540 'PRINT "[RDCH]";
550 RETURN
560 'Read and report CLI string
570 'PRINT "[OSCLI]";
580 T$ = ""
590 WHILE A<> &D
600   GOSUB 210
610   IF A>31 THEN T$ = T$+CHR$(A)
620 WEND
630 GOSUB 1000 : 'Tokenise the string
640 'PRINT "OSCLI Str ", A$(1)
650 IF A$(1)="LOAD" THEN GOTO 710
660 IF A$(1)="CAT" THEN CAT : GOTO 880
670 IF A$(1)="|B" THEN |B : GOTO 880
680 IF A$(1)="|A" THEN |A : GOTO 880
700 GOTO 880
710 T=&1 : GOSUB 380
720 T=&AA : GOSUB 380
730 T=&0 : GOSUB 380
740 T=&0 : GOSUB 380
750 T=&10 : GOSUB 380
760 T=&0 : GOSUB 380
770 T=&FF : GOSUB 380
780 PRINT "Loading ...";
790 RESTORE 10000
800 READ T
810 WHILE T<>-1
820   GOSUB 320
830   READ T
840 WEND
850 PRINT "DONE"
860 T=&5 : GOSUB 380
870 T=&AA : GOSUB 380
880 T=&7F : GOSUB 270
890 RETURN
900 'Read and handle OSWORD0
910 'PRINT "[OSWORD0]";
920 FOR B=1 TO 5
930   GOSUB 210
940 NEXT
950 T=&7F : GOSUB 270
960 INPUT T$
970 GOSUB 450
980 RETURN
990 'Tokenise string T$, max 16 words, return strings in A$()
1000 N = 1
1010 A$(N) = ""
1020 FOR L = 1 TO LEN(T$)
1030   C$ = MID$(T$, L, 1)
1040   IF C$<>"," THEN A$(N) = A$(N) + C$: GOTO 1060
1050   N = N + 1
1060 NEXT L
1070 RETURN
1080 |ASSEMBLE
1090 '.top
1100 'LD B,&FC
1110 'LD C,&10
1120 'IN A,(C)
1130 'RET P
1140 'LD C,&11
1150 'IN A,(C)
1160 'PUSH AF
1170 'CALL &BB5A
1180 'POP AF
1190 'JR NZ, top
1200 'LD B,&FC
1210 'LD C,&13
1220 'LD A,&7F
1230 'OUT (C),A
1240 'JR top
1250 'END
1260 RETURN
1270 '
10000 DATA -1

