10  'Amstrad CPC-Acorn Tube Host Code
20  '(C) 2018 Hoglet, BigEd, Revaldinho
30 MODE 2
40 INK 0,0 : INK 1,26 : BORDER 0
50 MEMORY HIMEM-128
60 fifo1 = HIMEM+1
70 GOSUB 920
80 OUT &FC16, 7  :' Select CPU
90 OUT &FC10,&AE :' Tube RESET
100 OUT &FC10,&20
110 WHILE 1=1
120   GOSUB 190
130   'PRINT HEX$(A,2);" ";
140   ' A=            0 ,   1 ,  2,    3  , 4 , 5 ,  6 , 7 , 8 , 9 , 10
150   ON (A+1) GOSUB 530, 500, 560, 500, 500,500, 500,500,500,500,840
160 WEND
170 END
180 ' Get FIFO R2 data in A without blocking FIFO R1 traffic
190 WHILE (INP(&FC12) AND &80)=0 
200   CALL fifo1
210 WEND
220 A =INP(&FC13)
230 RETURN
240 ' Write parameter T to FIFO R2 without blocking FIFO R1 traffic
250 WHILE (INP(&FC12) AND &40)=0 
260   CALL fifo1
270 WEND
280 OUT &FC13, T
290 RETURN
300 ' Write parameter T to FIFO R3 without blocking FIFO R1 traffic
310 WHILE (INP(&FC14) AND &40)=0 
320  CALL fifo1
330 WEND
340 OUT &FC15,T
350 RETURN
360 ' Write parameter T to FIFO R4 without blocking FIFO R1 traffic
370 WHILE (INP(&FC16) AND &40)=0 
380   CALL fifo1
390 WEND
400 OUT &FC17,T
410 RETURN
420 ' Write string parameter T$ to FIFO R2, terminated with &0D
430 FOR i=1 TO LEN(T$)
440   T= ASC(MID$(T$,i,1))
450   GOSUB 250
460 NEXT
470 T=13 : GOSUB 250
480 RETURN
490 'DUMMY Destination for unhandled calls
500 PRINT "[Unhandled Trap]"
510 RETURN
520 'RDCH
530 PRINT "[RDCH]";
540 RETURN
550 ' Read and report CLI string
560 PRINT "[OSCLI]";
570 CLISTR$ = ""
580 WHILE A<> &D
590   GOSUB 190
600 IF A>31 THEN CLISTR$ = CLISTR$+CHR$(A)
610 WEND
620 PRINT "OSCLI Str ", CLISTR$
630 IF CLISTR$<>"LOAD" THEN GOTO 800
640 T=&1 : GOSUB 360
650 T=&AA : GOSUB 360
660 T=&0 : GOSUB 360
670 T=&0 : GOSUB 360
680 T=&10 : GOSUB 360
690 T=&0 : GOSUB 360
700 T=&FF : GOSUB 360
710 '
720 PRINT "Loading ...";
730 RESTORE 10000
740 READ T
750 WHILE T<>-1
760   GOSUB 300
770  READ T
780 WEND
790 T=&5 : GOSUB 360
795 T=&AA : GOSUB 360
800 T=&7F : GOSUB 250
810 PRINT "DONE"
820 RETURN
830 ' Read and handle OSWORD0
840 PRINT "[OSWORD0]";
850 FOR B=1 TO 5
860   GOSUB 190
870 NEXT
880 T=&7F : GOSUB 250
890 INPUT T$
900 GOSUB 430
910 RETURN
920 |ASSEMBLE
930 '.top
940 'LD B,&FC
950 'LD C,&10
960 'IN A,(C)
970 'RET P
980 'LD C,&11
990 'IN A,(C)
1000 'PUSH AF
1010 'CALL &BB5A
1020 'POP AF
1030 'JR NZ, top
1040 'LD B,&FC
1050 'LD C,&13
1060 'LD A,&7F
1070 'OUT (C),A
1080 'JR top
1090 'END
1100 RETURN
1110 '
1120 '
10000 DATA -1

