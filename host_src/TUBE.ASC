10  'Amstrad CPC-Acorn Tube Host Code
20  '(C) 2018 Hoglet, BigEd, Revaldinho
30 DEFINT A-Z
40 DEFREAL f
50 DIM A$(16)
60 ON ERROR GOTO 120
70 MODE 2: INK 0,0 : INK 1,26 : BORDER 0
80 MEMORY HIMEM-128
90 fifo1=HIMEM+1
100 GOSUB 1170
110 OUT &FC16, 7  :'Select CPU
120 OUT &FC10,&AE :'Tube RESET
130 OUT &FC10,&20
140 WHILE 1=1
150   GOSUB 210
160   'PRINT HEX$(A,2);" ";
170   ON (A+1) GOSUB 540, 510, 570, 510, 510,510, 510,510,510,510,760
180 WEND
190 END
200 'Get FIFO R2 data in A without blocking FIFO R1 traffic
210 WHILE (INP(&FC12) AND &80)=0 
220   CALL fifo1
230 WEND
240 A=INP(&FC13)
250 RETURN
260 'Write parameter T to FIFO R2 without blocking FIFO R1 traffic
270 WHILE (INP(&FC12) AND &40)=0 
280   CALL fifo1
290 WEND
300 OUT &FC13, T
310 RETURN
320 'Write parameter T to FIFO R3 without blocking FIFO R1 traffic
330 WHILE (INP(&FC14) AND &40)=0 
340  CALL fifo1
350 WEND
360 OUT &FC15,T
370 RETURN
380 'Write parameter T to FIFO R4 without blocking FIFO R1 traffic
390 WHILE (INP(&FC16) AND &40)=0 
400   CALL fifo1
410 WEND
420 OUT &FC17,T
430 RETURN
440 'Write string parameter T$ to FIFO R2, terminated with &0D
450 FOR i=1 TO LEN(T$)
460   T=ASC(MID$(T$,i,1)) : GOSUB 270
470 NEXT
480 T=13 : GOSUB 270
490 RETURN
500 'DUMMY Destination for unhandled calls
510 PRINT "[Unhandled Trap]"
520 RETURN
530 'RDCH
540 'PRINT "[RDCH]";
550 RETURN
560 'Read and report CLI string
570 'PRINT "[OSCLI]";
580 T$=""
590 WHILE A<> &D
600   GOSUB 210
610   IF A>31 THEN T$=T$+CHR$(A)
620 WEND
630 GOSUB 850 : 'Tokenise the string
640 'PRINT "OSCLI Str ", A$(1)
650 IF A$(1)="LOAD" THEN GOSUB 940 : GOTO 730
660 IF A$(1)="CAT" THEN CAT : GOTO 730
670 IF A$(1)="|B" THEN |B : GOTO 730
680 IF A$(1)="|A" THEN |A : GOTO 730
730 T=&7F : GOSUB 270
740 RETURN
750 'Read and handle OSWORD0
760 'PRINT "[OSWORD0]";
770 FOR B=1 TO 5
780   GOSUB 210
790 NEXT
800 T=&7F : GOSUB 270
810 INPUT T$
820 GOSUB 450
830 RETURN
840 'Tokenise string T$, max 16 words, return strings in A$()
850 N=1
860 A$(N)=""
870 FOR L=1 TO LEN(T$)
880   C$=MID$(T$,L,1)
890   IF C$<>" " THEN A$(N)=A$(N)+C$: GOTO 910
900   N=N+1: A$(N)=""
910 NEXT L
915 WHILE I<16:A$(I)="":I=I+1:WEND
920 RETURN
930 ' LOAD <FILENAME> [ADDRESS:&1000] [WORDSIZE:&4]
940 PRINT "Loading File ";A$(2);
950 OPENIN A$(2)
960 IF A$(3)=""THEN ADR=&1000 ELSE ADR=VAL("&"+A$(3))
970 IF A$(4)=""THEN INC=4 ELSE INC=VAL(A$(4))
980 WHILE NOT EOF
990   LINE INPUT #9, H$
1000   IF MID$(H$,1,1)="#" THEN GOTO 1130
1010   T=&1 : GOSUB 380
1020   T=&AA : GOSUB 380
1030   T=&0 : GOSUB 380
1040   T=&0 : GOSUB 380
1050   T=INT(ADR/256) : GOSUB 380
1060   T=ADR AND &FF  : GOSUB 380
1070   T=&FF : GOSUB 380
1080   FOR I=1 TO LEN(H$) STEP 2
1090     T=VAL("&"+MID$(H$,I,2))
1100     GOSUB 320
1110   NEXT I
1111   PRINT ".";
1120   ADR=ADR+INT(LEN(H$)/(2*INC))
1130 WEND
1140 CLOSEIN
1142 T=&5 : GOSUB 380
1144 T=&AA : GOSUB 380
1150 PRINT "DONE"
1160 RETURN
1170 |ASSEMBLE
1180 '.top
1190 'LD B,&FC
1200 'LD C,&10
1210 'IN A,(C)
1220 'RET P
1230 'LD C,&11
1240 'IN A,(C)
1250 'PUSH AF
1260 'CALL &BB5A
1270 'POP AF
1280 'JR NZ, top
1290 'LD B,&FC
1300 'LD C,&13
1310 'LD A,&7F
1320 'OUT (C),A
1330 'JR top
1340 'END
1350 RETURN

